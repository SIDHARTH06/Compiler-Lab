//Write System Call

//Set the MODE FLAG field in the process table to the system call number which is 5 for write system call
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = 5;



//Store the value of user SP in a register as we need it for further computations.
alias userSP R0;
userSP = SP;


//Save the value of SP in the user SP field of Process Table entry of the process.
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 13]=userSP;

//Set the value of SP to beginning of the kernel stack.
SP=([PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 11]*512)-1;



//Get the file Descriptor  value from the stack.

alias physicalPageNum R1;
alias offset R2;
alias fileDescPhysicalAddr R3;
physicalPageNum = [PTBR + 2 * ((userSP - 4)/ 512)];
offset = (userSP - 4) % 512;
fileDescPhysicalAddr = (physicalPageNum * 512) + offset;
alias fileDescriptor R4;
fileDescriptor=[fileDescPhysicalAddr];



//Check whether the file descriptor is valid

if (fileDescriptor != -2)
then
	//code when argument 1 is not valid 
    // Return -1

    alias physicalAddrRetVal R5;
	physicalAddrRetVal = ([PTBR + 2 * ((userSP - 1) / 512)] * 512) + ((userSP - 1) % 512);
	[physicalAddrRetVal] = -1;

else
	//code when argument 1 is valid
   

    //Calculate physical address of the argument 2 and extract the value from it , which is the word to be printed to the console.
    alias word R5;
    word = [[PTBR + 2 * ((userSP - 3) / 512)] * 512 + ((userSP - 3) % 512)];

    //Print the word
    multipush(R0,R1,R2,R3,R4,R5);           //Saving Register Context
    R1=3;                                   //Store the function number of Terminal Write
    R2=[SYSTEM_STATUS_TABLE + 1];           //PID of the current process
    R3=word;                                //word to be printed
    call MOD_4;                             //Calling Device Manager Module

    //Ignore contents of R0 as terminal write does not Return anything

    multipop(R0,R1,R2,R3,R4,R5);            //Restore the Context

    





    //Set the Return value as 0 indicating success
    alias physicalAddrRetVal R6;
    physicalAddrRetVal = ([PTBR + 2 * (userSP - 1)/ 512] * 512) + ((userSP - 1) % 512);
    [physicalAddrRetVal] = 0;


endif;

// set back the value of SP to point to top of user stack
SP = userSP;

//Reset the MODE FLAG field in the process table to 0. Value 0 indicates that process is running in user mode.
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = 0;

//passing control back to user program
ireturn;
